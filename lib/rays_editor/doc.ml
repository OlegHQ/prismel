open Procedural

let flow_result result = Result.map_error Flow.Diagnostic.to_string result
let update_geometry edit document =
  Result.bind (edit document.Flow_sop.Network.geometry) (fun geometry ->
    flow_result (Flow_sop.Network.with_geometry geometry document))
let cook_effects = Parameter.add_impact Parameter.Cook Parameter.no_effects

(* Set parameters of one node (a scene object or World layer, or a lowered node). *)
let apply_parameters document ~node_id values =
  Result.bind (Edit_graph.apply_parameters document.Flow_sop.Network.geometry ~node_id values)
    (fun (geometry, effects) -> Result.map (fun document -> document, effects)
      (update_geometry (fun _ -> Ok geometry) document))

let relabel document ~node_id label = flow_result (Flow_sop.Network.relabel ~node_id label document)

(* One gesture on a workspace document: rewrite and re-check the source, then
   lower it into the document's objects.  Atomic: an error changes nothing. *)
let syntax_edit_result ~factories (doc : Editor_document.Document.t) op =
  let ( let* ) = Result.bind in
  let missing graph = not (List.exists (fun (g : Flow.Workspace.graph) -> g.name = graph)
    (fst doc.workspace).checked.graphs) in
  let run (doc : Editor_document.Document.t) =
    let* catalog = Editor_document.Contexts.catalog ~version:Flow_sop.Manifest.version factories in
    let* workspace = Editor_document.Workspace_doc.edit catalog (fst doc.workspace) op in
    Editor_document.Contexts.of_workspace ~factories ~previous:doc workspace in
  (* a gesture on the scene or the World the host composes (the text has no such graph yet, so
     the edit finds nothing to change): the host's own objects are written out first, and the
     gesture is tried on that text.  Kept only when it then checks. *)
  let adopted ~world = Result.bind (Result.map_error (Flow.Diagnostic.error ~code:"E_EDIT")
    (Editor_document.Scene_sync.adopt ~factories ~world doc)) run in
  match run doc with
  | Ok _ as done_ -> done_
  | Error _ as refused ->
      let retry = List.filter_map (fun (graph, world) ->
        if missing graph then Result.to_option (adopted ~world) else None)
        [ "scene", false; "world", true ] in
      (match retry with doc :: _ -> Ok doc | [] -> refused)

let syntax_edit ~factories (doc : Editor_document.Document.t) op =
  Result.map_error Flow.Diagnostic.to_string (syntax_edit_result ~factories doc op)

(* Several rewrites, one gesture: all or none.  The host's own objects are written out first
   when the batch adds to a scene graph the document does not have yet. *)
let syntax_batch ~factories (doc : Editor_document.Document.t) ops =
  let adds_to_scene = List.exists (function
    | Flow_graph.Flow_edit.Add_node { scope = [ "scene" ]; _ } -> true | _ -> false) ops in
  let missing = not (List.exists (fun (g : Flow.Workspace.graph) -> g.name = "scene")
    (fst doc.workspace).checked.graphs) in
  let first = if adds_to_scene && missing then Editor_document.Scene_sync.adopt ~factories ~world:false doc
    else Ok doc in
  List.fold_left (fun doc op -> Result.bind doc (fun doc -> syntax_edit ~factories doc op))
    (Result.map_error Fun.id first) ops

(* The whole workspace text, edited: parsed and checked as a
   document, lowered, atomic. *)
let text_edit ?imports ~factories (doc : Editor_document.Document.t) text =
  let workspace, _ = doc.workspace in
  let ( let* ) = Result.bind in
  let one result = Result.map_error (fun d -> [ d ]) result in
  let* catalog = one (Editor_document.Contexts.catalog ~version:Flow_sop.Manifest.version factories) in
  let* edited = Editor_document.Workspace_doc.of_text ~imports:(Option.value imports
      ~default:(Editor_document.Workspace_doc.import_texts workspace))
      ~ops:workspace.checked.ops ~settings:workspace.settings
      ~layout:workspace.layout ~inputs:workspace.inputs catalog text in
  one (Editor_document.Contexts.of_workspace ~factories ~previous:doc edited)

(* A change to the workspace's layout keys (a moved item, a collapsed zone):
   the source and the lowering are untouched, so nothing recooks. *)
let layout_edit (doc : Editor_document.Document.t) f =
  let workspace, lowered = doc.workspace in
  { doc with workspace = { workspace with layout = f workspace.layout }, lowered }

let select_layout ~factories (doc : Editor_document.Document.t) name =
  let workspace = fst doc.workspace in
  let workspace = { workspace with layout = { workspace.layout with editor = Some name } } in
  match Editor_document.Workspace_doc.editor_graph workspace with
  | None -> Error ("Unknown editor layout " ^ name ^ ".")
  | Some _ -> Editor_document.Contexts.of_workspace ~factories ~previous:doc workspace
      |> Result.map_error Flow.Diagnostic.to_string

(* A derived edit (an object's field, a reparent, a World layer) written to the text. *)
let reconcile ~factories ?adopt before after =
  Editor_document.Scene_sync.reconcile ~factories ?adopt before after

(* The binding of the panel at tree [path] (an assignable node for a gesture): a named panel is
   its binding, one written in place is bound first (the call holding it unfolded). *)
let panel_node ~factories ?(loop_message = "These panels are copies made by a loop: edit the loop in the editor graph.")
    (doc : Editor_document.Document.t) path =
  let ( let* ) = Result.bind in
  let graph = Option.map (fun (g : Flow.Workspace.graph) -> g.name)
    (Editor_document.Workspace_doc.editor_graph (fst doc.workspace)) in
  match graph, Option.bind doc.shell (fun s -> List.assoc_opt path s.Editor_document.Document.origins) with
  | Some graph, Some (Editor_document.Document.Bound name) -> Ok (doc, [ graph; name ])
  | _, Some (Inline (home, key)) ->
      let* doc, node = Editor_document.Scene_sync.bind_home ~factories doc home in
      let* doc = syntax_edit ~factories doc (Flow_graph.Flow_edit.Unfold { node; key; sub = [] }) in
      (match Flow_graph.Flow_edit.arg_text (fst doc.workspace).source node key with
       | Some { Flow.Syntax.node = Sym name; _ } -> Ok (doc, List.rev (name :: List.tl (List.rev node)))
       | _ -> Error "The panel could not be named.")
  | _, Some (Loop _) -> Error loop_message
  | _ -> Error "This panel is not part of an editor layout."
